<?xml version="1.0" encoding="utf-8"?>
<s:Application xmlns:fx="http://ns.adobe.com/mxml/2009" 
			   xmlns:s="library://ns.adobe.com/flex/spark" 
			   xmlns:mx="library://ns.adobe.com/flex/mx" width="1280" height="800" frameRate="30" backgroundColor="#000000" creationComplete="init()">
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>

	<fx:Script>
		<![CDATA[
			import com.greensock.TweenMax;
			import com.greensock.easing.Quad;
			import com.greensock.events.LoaderEvent;
			import com.greensock.loading.XMLLoader;
			import com.rgs.market.Bubble;
			import com.rgs.market.BubbleManager;
			import com.rgs.market.DateBug;
			import com.rgs.market.KeyboardManager;
			import com.rgs.market.PoemRetriever;
			import com.rgs.market.VideoLooper;
			
			import flash.display.LoaderInfo;
			
			import mx.core.FlexGlobals;
			
			import nl.demonsters.debugger.MonsterDebugger;
			
			private var settingsLoader : XMLLoader;
			private var settings : XML;
			
			private var todaysPoem : String;
			private var bm : BubbleManager;
			private var myPoemRetriever : PoemRetriever;
			private var debugger : MonsterDebugger;
			private var dateBug : DateBug;
			
			private var loop : VideoLooper;
			
			private var poemDate : Date;
			
			private function init():void
			{
				debugger = new MonsterDebugger(this);
				MonsterDebugger.clearTraces();
				
				addEventListener(Event.ADDED_TO_STAGE, onAddedToStage);
				
				settingsLoader = new XMLLoader("settings.xml", { onComplete: onSettingsLoaded });
				settingsLoader.load();
			}
			
			private function onAddedToStage(e:Event):void
			{
				removeEventListener(Event.ADDED_TO_STAGE, onAddedToStage);				
				this.stage.addEventListener(Event.RESIZE, onResize);
			}
			
			private function onResize(e:Event):void
			{
				if (loop) { loop.resize(); }
				if (bm) { bm.resize(); }
				if (dateBug) { dateBug.resize(); }
			}
			
			private function onSettingsLoaded(e:LoaderEvent):void
			{
				settings = settingsLoader.content;
				
				if (settings.poem.date.toString() == "today")
				{
					poemDate = new Date();
					poemDate.seconds = 0;
					poemDate.minutes = 0;
					poemDate.hours = 0;
					poemDate.milliseconds = 0;
				}
				else
				{
					poemDate = new Date(settings.poem.date.toString());
				}
				
				myPoemRetriever = new PoemRetriever(settings.account.username, settings.account.password, settings.account.calendarName);
				myPoemRetriever.gotPoemFallback.add(onPoemFallback);
				myPoemRetriever.gotPoemSignal.add(onPoemReceived);
				
				loop = new VideoLooper(settings.video.url, settings.video.width, settings.video.height, settings.video.xpos, settings.video.ypos, settings.video.loop);
				this.stage.addChild(loop);
				
				bm = new BubbleManager(settings.timing);
				this.stage.addChild(bm);
				loadPoem(poemDate);
				
				dateBug = new DateBug();
				this.stage.addChild(dateBug);
				
			
				setupKeyboardManager();
				
			}
			
			private function loadPoem(date:Date):void
			{
				MonsterDebugger.trace(this, "Getting poem for " + date);
				myPoemRetriever.getPoem(date);
			}
			
			private function onPoemReceived(thePoem:String):void
			{
				dateBug.text = poemDate;
				bm.poemText = thePoem;
				bm.startShow();
			}
			
			private function onPoemFallback(thePoem:String):void
			{
				MonsterDebugger.trace(this, ">>> poem fallback <<<", 0xff0000);
				dateBug.showError(poemDate);
				bm.poemText = thePoem;
				bm.startShow();
			}
			
			
			
			private function setupKeyboardManager():void
			{
				this.stage.addChild(KeyboardManager.getInstance());
				KeyboardManager.getInstance().restartSignal.add(onRestartKey);
				KeyboardManager.getInstance().loadSignal.add(onLoadKey);
				KeyboardManager.getInstance().nextSignal.add(onNextKey);
				KeyboardManager.getInstance().previousSignal.add(onPreviousKey);
			}
			
			private function onRestartKey():void
			{
				MonsterDebugger.trace(this, "restarting show");
				bm.showEnded.addOnce( function():void
				{
					bm.startShow();
				});
				bm.endShow();
			}
			
			private function onLoadKey():void
			{
				MonsterDebugger.trace(this, "got load key");
			}
			
			private function onNextKey():void
			{
				MonsterDebugger.trace(this, "got next key");
				
				bm.showEnded.addOnce
				(
					function():void
					{
						poemDate.date ++;
						loadPoem(poemDate);
					}
				);
				bm.endShow();
			}
			
			private function onPreviousKey():void
			{
				MonsterDebugger.trace(this, "got previous key");
				bm.endShow();
				poemDate.date --;
				loadPoem(poemDate);
			}
		]]>
	</fx:Script>
</s:Application>
